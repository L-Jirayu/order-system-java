plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'application'
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'com.example'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:1.62.2"
    implementation "io.grpc:grpc-protobuf:1.62.2"
    implementation "io.grpc:grpc-stub:1.62.2"
    compileOnly "javax.annotation:javax.annotation-api:1.3.2"
    testImplementation "junit:junit:4.13.2"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.3"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.62.2"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins { grpc {} }
        }
    }
}

application {
    mainClass = 'com.example.AppServer'
}

tasks.named("shadowJar") {
    archiveClassifier.set("all")
    mergeServiceFiles()
}

tasks.build {
    dependsOn tasks.shadowJar
}
